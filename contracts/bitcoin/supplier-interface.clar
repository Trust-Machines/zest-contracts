(use-trait lp-token .lp-token-trait.lp-token-trait)
(use-trait cp-token .distribution-token-cycles-losses-trait.distribution-token-cycles-losses-trait)
(use-trait dt .distribution-token-trait.distribution-token-trait)
(use-trait dtc .distribution-token-cycles-trait.distribution-token-cycles-trait)
(use-trait lv .liquidity-vault-trait.liquidity-vault-trait)
(use-trait cv .coll-vault-trait.coll-vault-trait)
(use-trait ft .ft-trait.ft-trait)
(use-trait v .vault-trait.vault-trait)
(use-trait fv .funding-vault-trait.funding-vault-trait)
(use-trait rewards-calc .rewards-calc-trait.rewards-calc-trait)

(use-trait payment .payment-trait.payment-trait)
(use-trait swap .swap-router-trait.swap-router-trait)

(define-data-var owner principal tx-sender)

;; tx-id -> tx-height
(define-map escrowed-tx (buff 32) uint)

(define-public (send-funds
    (block { header: (buff 80), height: uint })
    (prev-blocks (list 10 (buff 80)))
    (tx (buff 1024))
    (proof { tx-index: uint, hashes: (list 12 (buff 32)), tree-depth: uint })
    (output-index uint)
    (sender (buff 33))
    (recipient (buff 33))
    (expiration-buff (buff 4))
    (hash (buff 32))
    (swapper-buff (buff 4))
    (supplier-id uint)
    (min-to-receive uint)
  )
  (let (
    (tx-id (contract-call? .clarity-bitcoin get-txid tx))
  )
    (asserts! (map-insert escrowed-tx tx-id (get height block)) ERR_TX_ESCROWED)
    (print tx-id)
    (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge escrow-swap block prev-blocks tx proof output-index sender recipient expiration-buff hash swapper-buff supplier-id min-to-receive))
    (ok true)
  )
)

(define-public (send-funds-xbtc
    (factor uint)
    (lp-token <lp-token>)
    (token-id uint)
    (zp-token <dtc>)
    (lv <lv>)
    (xbtc-ft <ft>)
    (amount uint)
    (rewards-calc <rewards-calc>)
  )
  (let (
    (height burn-block-height)
    (globals (contract-call? .globals get-globals))
    )
    (asserts! (get contingency-plan globals) ERR_CONTINGENCY_PLAN_DISABLED)
    (try! (contract-call? .pool-v1-0 send-funds lp-token token-id zp-token amount factor height lv xbtc-ft rewards-calc contract-caller))
    (ok amount)
  )
)

(define-public (send-funds-finalize
  (txid (buff 32))
  (preimage (buff 128))
  (factor uint)
  (lp-token <lp-token>)
  (token-id uint)
  (zp-token <dtc>)
  (lv <lv>)
  (xbtc-ft <ft>)
  (rewards-calc <rewards-calc>)
  )
  (let (
      (swap (try! (finalize-priv txid preimage xbtc-ft)))
      (fee (get fee swap))
      (xbtc (get xbtc swap))
      (hash (get hash swap))
      (height (unwrap! (map-get? escrowed-tx txid) ERR_TX_DOES_NOT_EXIST))
      (swapper (get swapper-principal swap))
      ;; (full-amount (try! (transfer-full xbtc fee swapper .liquidity-vault)))
    )
      (try! (contract-call? .pool-v1-0 send-funds lp-token token-id zp-token (get sats swap) factor height lv xbtc-ft rewards-calc contract-caller))
      (map-delete escrowed-tx txid)
      (ok (get sats swap))
  )
)

(define-public (send-funds-wrap
  (block { header: (buff 80), height: uint })
  (prev-blocks (list 10 (buff 80)))
  (tx (buff 1024))
  (proof { tx-index: uint, hashes: (list 12 (buff 32)), tree-depth: uint })
  (output-index uint)
  (sender (buff 33))
  (recipient (buff 33))
  (expiration-buff (buff 4))
  (hash (buff 32))
  (swapper-buff (buff 4))
  (supplier-id uint)
  (min-to-receive uint)
  ;; second part
  (preimage (buff 128))
  (factor uint)
  (lp-token <lp-token>)
  (token-id uint)
  (zp-token <dtc>)
  (lv <lv>)
  (xbtc-ft <ft>)
  (rewards-calc <rewards-calc>)
  )
  (let (
    (tx-id (contract-call? .clarity-bitcoin get-txid tx))
  )
    (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge escrow-swap block prev-blocks tx proof output-index sender recipient expiration-buff hash swapper-buff supplier-id min-to-receive))
    (let (
      (swap (try! (finalize-priv tx-id preimage xbtc-ft)))
      (fee (get fee swap))
      (xbtc (get xbtc swap))
      (swapper (get swapper-principal swap))
    )
      (try! (contract-call? .pool-v1-0 send-funds lp-token token-id zp-token (get sats swap) factor (get height block) lv xbtc-ft rewards-calc contract-caller))
      (ok (get sats swap))
    )
  )
)

(define-private (finalize-priv (txid (buff 32)) (preimage (buff 128)) (xbtc-ft <ft>))
  (let (
    (swap-resp (as-contract (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge finalize-swap txid preimage))))
    (swap (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge get-full-inbound txid)))
    (swapper (get swapper-principal swap))
    (sats (get sats swap))
    (xbtc (get xbtc swap))
    (fee (- sats xbtc))
    (updated-funds (try! (withdraw-bridge fee)))
  )
    (try! (as-contract (contract-call? xbtc-ft transfer fee tx-sender swapper none)))
    (asserts! (is-eq contract-caller swapper) ERR_WRONG_SWAPPER)
    (ok (merge swap { fee: fee }))
  )
)

(define-public (make-payment
  (txid (buff 32))
  (preimage (buff 128))
  (loan-id uint)
  (payment <payment>)
  (lp-token <lp-token>)
  (lv <lv>)
  (token-id uint)
  (cp-token <cp-token>)
  (cp-rewards-token <dt>)
  (zp-token <dt>)
  (swap-router <swap>)
  (xbtc-ft <ft>)
  )
  (let (
    (swap (try! (finalize-priv txid preimage xbtc-ft)))
    (fee (get fee swap))
    (xbtc (get xbtc swap))
    (hash (get hash swap))
    (height (unwrap! (map-get? escrowed-tx txid) ERR_TX_DOES_NOT_EXIST))
    (swapper (get swapper-principal swap))
  )
    (map-delete escrowed-tx txid)
    (ok (try! (contract-call? .loan-v1-0 make-payment loan-id height payment lp-token lv token-id cp-token cp-rewards-token zp-token swap-router (get sats swap) xbtc-ft contract-caller)))
  )
)

(define-public (make-residual-payment
  (txid (buff 32))
  (preimage (buff 128))
  (loan-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (xbtc-ft <ft>)
  )
  (let (
    (swap (try! (finalize-priv txid preimage xbtc-ft)))
    (fee (get fee swap))
    (xbtc (get xbtc swap))
    (hash (get hash swap))
    (height (unwrap! (map-get? escrowed-tx txid) ERR_TX_DOES_NOT_EXIST))
    (swapper (get swapper-principal swap))
  )
    (map-delete escrowed-tx txid)
    (ok (try! (contract-call? .pool-v1-0 make-residual-payment loan-id lp-token token-id (get sats swap) xbtc-ft contract-caller)))
  )
)

(define-public (make-full-payment
  (txid (buff 32))
  (preimage (buff 128))
  (loan-id uint)
  (payment <payment>)
  (lp-token <lp-token>)
  (lv <lv>)
  (token-id uint)
  (cp-token <cp-token>)
  (cp-rewards-token <dt>)
  (zp-token <dt>)
  (swap-router <swap>)
  (xbtc-ft <ft>)
  )
  (let (
    (swap (try! (finalize-priv txid preimage xbtc-ft)))
    (fee (get fee swap))
    (xbtc (get xbtc swap))
    (hash (get hash swap))
    (height (unwrap! (map-get? escrowed-tx txid) ERR_TX_DOES_NOT_EXIST))
    (swapper (get swapper-principal swap))
  )
    (map-delete escrowed-tx txid)
    (ok (try! (contract-call? .loan-v1-0 make-full-payment loan-id height payment lp-token lv token-id cp-token cp-rewards-token zp-token swap-router (get sats swap) xbtc-ft contract-caller)))
  )
)

(define-public (make-payment-xbtc
  (amount uint)
  (loan-id uint)
  (payment <payment>)
  (lp-token <lp-token>)
  (lv <lv>)
  (token-id uint)
  (cp-token <cp-token>)
  (cp-rewards-token <dt>)
  (zp-token <dt>)
  (swap-router <swap>)
  (xbtc-ft <ft>)
  )
  (let (
    (height burn-block-height)
    (globals (contract-call? .globals get-globals))
  )
    (asserts! (get contingency-plan globals) ERR_CONTINGENCY_PLAN_DISABLED)
    (contract-call? .loan-v1-0 make-payment loan-id height payment lp-token lv token-id cp-token cp-rewards-token zp-token swap-router amount xbtc-ft contract-caller)
  )
)

(define-private (transfer-full (swapped-amount uint) (fee uint) (sender principal) (recipient principal) (xbtc <ft>))
  (begin
    (try! (as-contract (contract-call? xbtc transfer swapped-amount sender recipient none)))
    (try! (as-contract (contract-call? xbtc transfer fee tx-sender recipient none)))
    (ok (+ swapped-amount fee))
  )
)

(define-public (make-full-payment-xbtc
  (amount uint)
  (loan-id uint)
  (payment <payment>)
  (lp-token <lp-token>)
  (lv <lv>)
  (token-id uint)
  (cp-token <cp-token>)
  (cp-rewards-token <dt>)
  (zp-token <dt>)
  (swap-router <swap>)
  (xbtc-ft <ft>)
  )
  (let (
    (globals (contract-call? .globals get-globals))
  )
    (asserts! (get contingency-plan globals) ERR_CONTINGENCY_PLAN_DISABLED)
    (ok (try! (contract-call? .loan-v1-0 make-full-payment loan-id burn-block-height payment lp-token lv token-id cp-token cp-rewards-token zp-token swap-router amount xbtc-ft contract-caller)))
  )
)

(define-public (withdraw
  (xbtc uint)
  (btc-version (buff 1))
  (btc-hash (buff 20))
  (supplier-id uint)
  (lp-token <lp-token>)
  (zp-token <dtc>)
  (token-id uint)
  (lv <lv>)
  (xbtc-ft <ft>)
  )
  (let (
    (pool (try! (contract-call? .pool-v1-0 get-pool token-id)))
  )
    (if (get open pool)
      true
      (asserts! (contract-call? .globals is-onboarded-address-read contract-caller btc-version btc-hash) ERR_ADDRESS_NOT_ALLOWED)
    )
    (try! (contract-call? .pool-v1-0 withdraw lp-token zp-token token-id lv xbtc xbtc-ft contract-caller))
    (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge initiate-outbound-swap xbtc btc-version btc-hash supplier-id))
    (print { btc-version: btc-version, btc-hash: btc-hash, supplier-id: supplier-id, amount: xbtc })
    (ok true)
  )
)

(define-public (withdraw-xbtc
  (xbtc uint)
  (lp-token <lp-token>)
  (zp-token <dtc>)
  (token-id uint)
  (lv <lv>)
  (xbtc-ft <ft>)
  )
  (let (
    (globals (contract-call? .globals get-globals))
  )
    (asserts! (get contingency-plan globals) ERR_CONTINGENCY_PLAN_DISABLED)
    (try! (contract-call? .pool-v1-0 withdraw lp-token zp-token token-id lv xbtc xbtc-ft contract-caller))
    (ok true)
  )
)

(define-public (withdraw-rewards
  (btc-version (buff 1))
  (btc-hash (buff 20))
  (supplier-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (lv <lv>)
  (xbtc <ft>)
  )
  (let (
    (withdrawn-funds (try! (contract-call? .pool-v1-0 withdraw-rewards lp-token token-id lv xbtc contract-caller)))
    (pool (try! (contract-call? .pool-v1-0 get-pool token-id)))
  )
    (if (get open pool)
      true
      (asserts! (contract-call? .globals is-onboarded-address-read contract-caller btc-version btc-hash) ERR_ADDRESS_NOT_ALLOWED)
    )
    (print { btc-version: btc-version, btc-hash: btc-hash, supplier-id: supplier-id, amount: withdrawn-funds })
    ;; (try! (contract-call? .xbtc transfer withdrawn-funds tx-sender (as-contract tx-sender) none))
    (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge initiate-outbound-swap withdrawn-funds btc-version btc-hash supplier-id))
    (ok withdrawn-funds)
  )
)

(define-public (withdraw-cover-rewards
  (btc-version (buff 1))
  (btc-hash (buff 20))
  (supplier-id uint)
  (cp-rewards-token <dt>)
  (token-id uint)
  (lv <lv>)
  (xbtc <ft>)
  )
  (let (
    (withdrawn-funds (try! (contract-call? .cover-pool-v1-0 withdraw-rewards cp-rewards-token token-id lv xbtc contract-caller)))
    (pool (try! (contract-call? .pool-v1-0 get-pool token-id)))
  )
    (if (get open pool)
      true
      (asserts! (contract-call? .globals is-onboarded-address-read contract-caller btc-version btc-hash) ERR_ADDRESS_NOT_ALLOWED)
    )
    (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge initiate-outbound-swap withdrawn-funds btc-version btc-hash supplier-id))
    (ok withdrawn-funds)
  )
)

(define-public (withdraw-cover-rewards-xbtc
  (btc-version (buff 1))
  (btc-hash (buff 20))
  (supplier-id uint)
  (cp-rewards-token <dt>)
  (token-id uint)
  (lv <lv>)
  (xbtc <ft>)
  )
  (let (
    (globals (contract-call? .globals get-globals))
    (withdrawn-funds (try! (contract-call? .cover-pool-v1-0 withdraw-rewards cp-rewards-token  token-id lv xbtc contract-caller)))
  )
    (asserts! (get contingency-plan globals) ERR_CONTINGENCY_PLAN_DISABLED)
    (ok withdrawn-funds)
  )
)

(define-public (withdraw-rewards-xbtc
  (lp-token <lp-token>)
  (token-id uint)
  (lv <lv>)
  (xbtc <ft>)
  )
  (let (
    (globals (contract-call? .globals get-globals))
    (withdrawn-funds (try! (contract-call? .pool-v1-0 withdraw-rewards lp-token token-id lv xbtc contract-caller)))
  )
    (asserts! (get contingency-plan globals) ERR_CONTINGENCY_PLAN_DISABLED)
    (ok withdrawn-funds)
  )
)

(define-public (drawdown
  (loan-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (coll-token <ft>)
  (coll-vault <cv>)
  (fv <v>)
  (btc-version (buff 1))
  (btc-hash (buff 20))
  (supplier-id uint)
  (swap-router <swap>)
  (xbtc-ft <ft>)
  )
  (let (
    (pool (try! (contract-call? .pool-v1-0 get-pool token-id)))
    (xbtc (try! (contract-call? .pool-v1-0 drawdown loan-id lp-token token-id coll-token coll-vault fv swap-router xbtc-ft contract-caller)))
    (swap-id (try! (as-contract (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge initiate-outbound-swap xbtc btc-version btc-hash supplier-id))))
    (liquidity (try! (get-current-liquidity)))
  )
    (asserts! (contract-call? .globals is-onboarded-address-read contract-caller btc-version btc-hash) ERR_ADDRESS_NOT_ALLOWED)
    ;; (print { liquidity: liquidity, result: (>= liquidity xbtc) })
    (asserts! (>= liquidity xbtc) ERR_NOT_ENOUGH_LIQUIDITY)
    (print { btc-version: btc-version, btc-hash: btc-hash, supplier-id: supplier-id, amount: xbtc })
    (ok { swap-id: swap-id, sats: xbtc })
  )
)

(define-public (finalize-drawdown
  (loan-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (coll-token <ft>)
  (coll-vault <cv>)
  (fv <v>)
  (xbtc-ft <ft>)
  (block { header: (buff 80), height: uint })
  (prev-blocks (list 10 (buff 80)))
  (tx (buff 1024))
  (proof { tx-index: uint, hashes: (list 12 (buff 32)), tree-depth: uint })
  (output-index uint)
  (swap-id uint)
  )
  (begin
    (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge finalize-outbound-swap block prev-blocks tx proof output-index swap-id))
    (contract-call? .pool-v1-0 finalize-drawdown loan-id lp-token token-id coll-token coll-vault fv xbtc-ft)
  )
)

(define-public (complete-rollover
  (loan-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (coll-token <ft>)
  (coll-vault <cv>)
  (fv <v>)
  (btc-version (buff 1))
  (btc-hash (buff 20))
  (supplier-id uint)
  (swap-router <swap>)
  (xbtc-ft <ft>)
  )
  (let (
    (pool (try! (contract-call? .pool-v1-0 get-pool token-id)))
    (xbtc (try! (contract-call? .pool-v1-0 complete-rollover loan-id lp-token token-id coll-token coll-vault fv swap-router xbtc-ft contract-caller)))
    (swap-id (if (> xbtc u0) (try! (as-contract (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge initiate-outbound-swap xbtc btc-version btc-hash supplier-id))) u0) )
    (liquidity (try! (get-current-liquidity)))
  )
    (asserts! (contract-call? .globals is-onboarded-address-read contract-caller btc-version btc-hash) ERR_ADDRESS_NOT_ALLOWED)
    (asserts! (>= liquidity xbtc) ERR_NOT_ENOUGH_LIQUIDITY)
    (ok swap-id)
  )
)

(define-public (finalize-rollover
  (loan-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (coll-token <ft>)
  (coll-vault <cv>)
  (fv <v>)
  (xbtc-ft <ft>)
  (block { header: (buff 80), height: uint })
  (prev-blocks (list 10 (buff 80)))
  (tx (buff 1024))
  (proof { tx-index: uint, hashes: (list 12 (buff 32)), tree-depth: uint })
  (output-index uint)
  (swap-id uint)
  )
  (begin
    (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge finalize-outbound-swap block prev-blocks tx proof output-index swap-id))
    (contract-call? .pool-v1-0 finalize-rollover loan-id lp-token token-id coll-token coll-vault fv xbtc-ft)
  )
)

(define-public (cancel-drawdown
  (loan-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (coll-token <ft>)
  (coll-vault <cv>)
  (fv <fv>)
  (xbtc-ft <ft>)
  (swap-id uint)
  )
  (let (
    (swap (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge revoke-expired-outbound swap-id)))
    (recovered-amount (get xbtc swap))
  )
    ;; (try! (as-contract (contract-call? fv add-asset xbtc-ft recovered-amount loan-id tx-sender)))
    (try! (as-contract (contract-call? xbtc-ft transfer recovered-amount tx-sender .pool-v1-0 none)))
    (try! (contract-call? .pool-v1-0 cancel-drawdown loan-id lp-token token-id coll-token coll-vault fv recovered-amount xbtc-ft))
    (ok (get xbtc swap))
  )
)

(define-public (cancel-rollover
  (loan-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (coll-token <ft>)
  (coll-vault <cv>)
  (fv <fv>)
  (xbtc-ft <ft>)
  (swap-id uint)
  )
  (let (
    (swap (try! (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge revoke-expired-outbound swap-id)))
    (recovered-amount (get xbtc swap))
  )
    ;; (try! (as-contract (contract-call? xbtc-ft transfer recovered-amount tx-sender (contract-of fv) none)))
    (try! (as-contract (contract-call? xbtc-ft transfer recovered-amount tx-sender .pool-v1-0 none)))
    (try! (contract-call? .pool-v1-0 cancel-rollover loan-id lp-token token-id coll-token coll-vault fv recovered-amount xbtc-ft contract-caller))
    (ok swap)
  )
)

(define-public (drawdown-xbtc
  (loan-id uint)
  (lp-token <lp-token>)
  (token-id uint)
  (coll-token <ft>)
  (coll-vault <cv>)
  (fv <v>)
  (swap-router <swap>)
  (xbtc-ft <ft>)
  )
  (let (
    (globals (contract-call? .globals get-globals))
    (xbtc (try! (contract-call? .pool-v1-0 drawdown loan-id lp-token token-id coll-token coll-vault fv swap-router xbtc-ft contract-caller)))
    (result (try! (contract-call? .pool-v1-0 finalize-drawdown loan-id lp-token token-id coll-token coll-vault fv xbtc-ft)))
  )
    (asserts! (get contingency-plan globals) ERR_CONTINGENCY_PLAN_DISABLED)
    (as-contract (try! (contract-call? xbtc-ft transfer xbtc tx-sender (get borrower result) none)))
    (ok { recipient: (get borrower result), sats: xbtc })
  )
)

(define-public (finalize-outbound
  (block { header: (buff 80), height: uint })
  (prev-blocks (list 10 (buff 80)))
  (tx (buff 1024))
  (proof { tx-index: uint, hashes: (list 12 (buff 32)), tree-depth: uint })
  (output-index uint)
  (swap-id uint)
  )
  (begin
    (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge finalize-outbound-swap block prev-blocks tx proof output-index swap-id)
  )
)

;; owner methods

(define-public (register-supplier
    (public-key (buff 33))
    (inbound-fee (optional int))
    (outbound-fee (optional int))
    (outbound-base-fee int)
    (inbound-base-fee int)
    (name (string-ascii 18))
    (funds uint)
  )
  (begin
    (try! (validate-owner))
    (as-contract (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge register-supplier public-key inbound-fee outbound-fee outbound-base-fee inbound-base-fee funds))
  )
)

(define-public (add-funds (amount uint))
  (begin
    (try! (validate-owner))
    (as-contract (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge add-funds amount))
  )
)

(define-public (remove-funds (amount uint))
  (begin
    (try! (validate-owner))
    (as-contract (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge remove-funds amount))
  )
)

(define-public (update-supplier
    (public-key (buff 33))
    (inbound-fee (optional int))
    (outbound-fee (optional int))
    (outbound-base-fee int)
    (inbound-base-fee int)
    (name (string-ascii 18))
  )
  (begin
    (try! (validate-owner))
    (try! (as-contract (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge update-supplier-fees inbound-fee outbound-fee outbound-base-fee inbound-base-fee)))
    (as-contract (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge update-supplier-public-key public-key))
  )
)

(define-public (transfer-owner (new-owner principal))
  (begin
    (try! (validate-owner))
    (var-set owner new-owner)
    (ok new-owner)
  )
)

;; internal

(define-private (withdraw-bridge (amount uint))
  (as-contract (contract-call? 'SP3NHG9CBN9SPH68HD8HGPS7F7499KCAEC9K20NZZ.bridge remove-funds amount))
)

;; helpers

(define-read-only (validate-owner)
  (if (is-eq contract-caller (get-owner))
    (ok true)
    ERR_UNAUTHORIZED
  )
)

(define-read-only (get-owner) (var-get owner))

(define-map bridge-liquidity uint uint)

(define-data-var highest-set-liquidity uint burn-block-height)

(define-read-only (get-current-liquidity)
  (get-liquidity (var-get highest-set-liquidity))
)

(define-read-only (get-liquidity-read (height uint))
  (unwrap-panic (map-get? bridge-liquidity height))
)

(define-read-only (get-liquidity (height uint))
  (ok (unwrap! (map-get? bridge-liquidity height) ERR_HEIGHT_UNAVAILABLE))
)

(define-public (update-liquidity (height uint) (liquidity uint))
  (begin
    (asserts! (contract-call? .globals is-governor contract-caller) ERR_UNAUTHORIZED)
    (if (> height (var-get highest-set-liquidity)) (var-set highest-set-liquidity height) false)
    (print { event: "updated_liquidity", height: height, liquidity: liquidity })
    (ok (map-set bridge-liquidity height liquidity))
  )
)

;; ERROR START 1000
(define-constant ERR_UNAUTHORIZED (err u1000))
(define-constant ERR_WRONG_SWAPPER (err u1001))
(define-constant ERR_PANIC (err u1002))
(define-constant ERR_TX_ESCROWED (err u1003))
(define-constant ERR_TX_DOES_NOT_EXIST (err u1004))
(define-constant ERR_HEIGHT_UNAVAILABLE (err u1005))
(define-constant ERR_NOT_ENOUGH_LIQUIDITY (err u1006))
(define-constant ERR_ADDRESS_NOT_ALLOWED (err u1007))
(define-constant ERR_CONTINGENCY_PLAN_DISABLED (err u1008))

